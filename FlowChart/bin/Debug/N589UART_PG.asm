























  
  
 USER_RAM: SECTION
R_UART_PORT_VALUE DS 1
R_UART_PORT_MODE DS 1
R_UART_TX_DATA  DS 1 
R_UART_RX_DATA  DS 1
R_UART_TXRX_STEP DS 1 
R_UART_TX_PT  DS 1 
R_UART_TX_SET  DS 1 
C_UART_TX_TOTAL_SET EQU 3 



C_UART_TMRA_C EQU 07H
C_UART_TMRA_V EQU 16 
C_UART_TX_FINISH EQU 9 
C_UART_TX_WAIT_TIME EQU 8 



 .ENDS
 
 CODE: SECTION
  
  




F_UART_PORT_INIT:
 LDA #C_UART_TX_PIN
 TSB !C_UART_TX_PORT_DATA
 TRB !C_UART_TX_PORT_DIR
 TSB !C_UART_TX_PORT_MODE
 
 
.IFDEF C_UART_RECEIVE_EN
 LDA #C_UART_RX_PIN
 STA !C_UART_RX_PORT_EN
 LDA !IEF0
 ORA #INT0_PORT
 STA !IEF0 
.ENDIF  

 RTS




F_UART_INIT:
 LDA !IEF0
.IFDEF C_UART_RECEIVE_EN 
 AND #~INT0_PORT
.ENDIF   
 ORA #INT0_TMA
 STA !IEF0
 LDA #C_UART_TMRA_V
 STA !TMAV
 LDA #C_UART_TMRA_C
 STA !TMAC
 
 RTS
 




F_UART_SEND_DATA: 
 
 
 JSR F_UART_INIT
 
 STZ R_UART_TX_PT
 STZ R_UART_TX_SET

 LDA R_SAVEX_BASE
 STA R_UART_TX_DATA 



  
 STZ R_UART_TXRX_STEP

 LDA #C_UART_TX_PIN
 TRB C_UART_TX_PORT_DATA
 
 
 LDA #C_UART_DATA_SENDING
 STA R_UART_PORT_MODE
 
 RTS
 







F_UART_TIMERA_ISR:
 LDA R_UART_PORT_MODE
 BIT #C_UART_DATA_SENDING
 BNE L_UART_SENDING
 BIT #C_UART_WAIT_SEND
 BNE L_UART_WAITING
.IFDEF C_UART_RECEIVE_EN   
 BIT #C_UART_RECEIVING
 BEQ L_UART_TIMERA_EXIT
 JMP L_UART_RECEIVING_DATA
.ENDIF 
L_UART_TIMERA_EXIT: 
 RTS

L_UART_WAITING:
 INC R_UART_TXRX_STEP
 LDA R_UART_TXRX_STEP
 CMP #C_UART_TX_WAIT_TIME
 BCC L_UART_WAITING_EXIT
 
 LDA #C_UART_DATA_SENDING
 STA R_UART_PORT_MODE
 STZ R_UART_TXRX_STEP
 BRA L_UART_TX_LOW 
L_UART_WAITING_EXIT: 
 RTS
 

L_UART_SENDING:
 INC R_UART_TXRX_STEP
 LDA R_UART_TXRX_STEP 
 LSR A
 BCS L_UART_TIMERA_EXIT 
 CMP #C_UART_TX_FINISH
 BCS L_UART_TX_SENT1BYTE
 
 LSR R_UART_TX_DATA
 BCS L_UART_TX_HIGH
 
L_UART_TX_LOW:
 LDA #C_UART_TX_PIN
 TRB C_UART_TX_PORT_DATA

 BRA L_UART_TX_WRITEPORT
 
L_UART_TX_SENT1BYTE: 
 
 INC R_UART_TX_SET
 LDA R_UART_TX_SET
 CMP #C_UART_TX_TOTAL_SET
 BCC L_UART_TX_WAIT_NEXTBYTE
 STZ R_UART_TX_SET
 INC R_UART_TX_PT
 LDX R_UART_TX_PT 
 CPX #C_GDATA_STOTAL 
 BCC L_UART_TX_WAIT_NEXTBYTE

 
 STZ !TMAC
 LDA !IEF0
 AND #~INT0_TMA
.IFDEF C_UART_RECEIVE_EN 
 ORA #INT0_PORT
.ENDIF 
 STA !IEF0 
 STZ R_UART_PORT_MODE



 
 BRA L_UART_TX_HIGH
 
L_UART_TX_WAIT_NEXTBYTE: 
 LDA #C_UART_WAIT_SEND
 STA R_UART_PORT_MODE
 STZ R_UART_TXRX_STEP

 LDX R_UART_TX_PT 
 LDA R_UART_TX_SET
 BEQ L_LOAD_SETX_DATA
 CMP #1
 BEQ L_LOAD_SETY_DATA
 LDA R_SAVEZ_BASE,X
 BRA L_SAVE_TX_DATA
L_LOAD_SETX_DATA: 
 LDA R_SAVEX_BASE,X
 BRA L_SAVE_TX_DATA
L_LOAD_SETY_DATA: 
 LDA R_SAVEY_BASE,X
L_SAVE_TX_DATA: 
 STA R_UART_TX_DATA 
L_UART_TX_HIGH:
 LDA #C_UART_TX_PIN
 TSB C_UART_TX_PORT_DATA
 
L_UART_TX_WRITEPORT: 

 RTS
 
 

.IFDEF C_UART_RECEIVE_EN  
L_UART_RECEIVING_DATA:
 INC R_UART_TXRX_STEP 
 
 LDA R_UART_TXRX_STEP
 LSR A
 BCC L_UART_RECEIVING_EXIT
 
 BEQ L_UART_RECEIVING_EXIT
 
 CMP #C_UART_TX_FINISH
 BCS L_UART_RECEIVED_DATA 
 
 LDA !C_UART_TX_PORT_DATA
 AND #C_UART_RX_PIN
 BEQ L_UART_SAVE_BITLOW
 
 SEC  
L_UART_SAVE_BITLOW: 
 ROR R_UART_RX_DATA
L_UART_RECEIVING_EXIT:
 RTS
 
L_UART_RECEIVED_DATA: 
 LDA #C_UART_RECEIVED 
 STA R_UART_PORT_MODE
 
 STZ !TMAC
 LDA !IEF0
 AND #~INT0_TMA
 ORA #INT0_PORT
 STA !IEF0 
 RTS
 
 
F_UART_PORT_ISR:
 LDA !C_UART_TX_PORT_DATA
 AND #C_UART_RX_PIN
 BEQ L_UART_RX_START_BIT
 RTS
 
L_UART_RX_START_BIT: 
 LDA !IEF0
 AND #~INT0_PORT
 ORA #INT0_TMA
 STA !IEF0 
 STZ R_UART_TXRX_STEP
 
 LDA #C_UART_TMRA_V
 STA !TMAV
 LDA #C_UART_TMRA_C
 STA !TMAC
 LDA #C_UART_RECEIVING
 STA R_UART_PORT_MODE
 RTS
.ENDIF 
 
 ENDS
    
